// Load all global variables from the jenkins-shared-library for Unity
// see https://github.com/burnedikt/jenkins-shared-library-unity for source
@Library('jenkins-shared-library-unity@v1.0') _

pipeline
{
    agent
    {
        label 'unity-ng'
    }
    
    parameters 
    {
        string(name: 'UNITY_VERSION', defaultValue: '2019.3.0f6', description: 'The Unity Version to build for')
        booleanParam(name: 'SKIP_TESTS', defaultValue: false, description: 'If set to true, the test will be skipped')
        booleanParam(name: 'BUILD_UNITYPACKAGE', defaultValue: true, description: 'If set to true, a unity package containing the SDK will be created and attached as artifact.')
        booleanParam(name: 'BUILD_DOCUMENTATION', defaultValue: false, description: 'If set to true, the Hub documentation will be built and attached as artifact.')        
    }

    options
    {
        disableConcurrentBuilds()
        skipDefaultCheckout true
    }

    environment
    {
        // Unity project base path
        PROJECT_PATH = "z:\\${BUILD_TAG}"
        // Path where the SDK should be checked out to
        INNOACTIVE_PATH = "${PROJECT_PATH}\\Assets\\Innoactive"
        // Path to the module within the Unity project.
        MODULE_DIRECTORY_IN_ASSETS = "${INNOACTIVE_PATH}\\Creator\\Core"
        // Path to the file with shared methods.
        SHARED = "${WORKSPACE}\\.JenkinsFile-Shared"
        // Path to U3D rules
        // U3D commands need to use a dedicated set of rules for logging to get more verbose output from e.g. Common.Logging,
        // see more at https://github.com/DragonBox/u3d/blob/master/LOG_RULES.md
        U3D_RULES_PATH = "z:\\u3d_log_rules.json"
        // Path to export config file.
        EXPORT_CONFIG = "z:\\.export-config"
    }

    stages
    {
        stage('Setup')
        {
            steps
            {
                // Initial clean up
                powershell "Remove-Item .\\* -Force -Recurse -ErrorAction SilentlyContinue"    
                powershell "git config --system core.longpaths true"
                checkout scm

                script
                {
                    load(SHARED).InitialSetup()                
                }
            }
        }

        stage('Initialization')
        {
            steps
            {
                script
                {
                    load(SHARED).InitialTrainingSetup()
                    load(SHARED).SetupUnity()

                    def file = readJSON file: '.dependencies.json'

                    load(SHARED).CopyTrainingModule(MODULE_DIRECTORY_IN_ASSETS)
                    
                    for (int i = 0; i < file.dependencies.size(); ++i) 
                    {
                        def dependency = file.dependencies[i]
                        load(SHARED).CheckoutComponent(dependency.repository, dependency.branch, "z:\\${BUILD_TAG}\\Assets\\Innoactive\\Creator\\${dependency.name}")
                    }
                }
            }
        }

        stage('Build Unity') 
        {
            steps
            {
                script
                {
                    load(SHARED).Build()
                }
            }
        }
        
        stage('Test')
        {
            when { expression { params.SKIP_TESTS == false } }
            steps
            {
                script
                {
                    load(SHARED).Test()
                }
            }
        }

        stage('Documentation')
        {
            when
            {
                anyOf 
                {
                    tag pattern: "v(\\d+\\.\\d+\\.\\d+(-docu|-preview)?)", comparator: "REGEXP"
                    expression { params.BUILD_DOCUMENTATION == true }
                }
            }
            stages
            {
                stage ('Source Documentation Theme Files')
                {
                    steps
                    {
                        dir("${env.INNOACTIVE_PATH}\\Creator\\Core\\.Documentation\\theme")
                        {
                            stash name: 'docfx-theme-raw', includes: "**"
                        }
                    }
                }
                stage ('Build Public Documentation Theme')
                {
                    agent
                    {
                        docker
                        {
                            image 'node:lts-slim'
                        }
                    }
                    steps
                    {
                        unstash name: 'docfx-theme-raw'
                        sh """
                        npm install
                        npm run build
                        """
                        stash name: 'docfx-theme', includes: 'dist/**'
                    }
                }
                stage ('Build Documentation')
                {
                    steps
                    {
                        dir("${env.INNOACTIVE_PATH}\\Creator\\Core\\.Documentation\\theme")
                        {
                            // Pop the built documentation theme
                            unstash name: 'docfx-theme'
                        }
                        script
                        {
                            load(SHARED).Documentation()
                        }
                    }
                }
                stage('Deploy Documentation')
                {
                    when
                    {
                        tag pattern: "v(\\d+\\.\\d+\\.\\d+(-docu|-preview)?)", comparator: "REGEXP"
                    }
                    steps
                    {
                        script
                        {
                            load(SHARED).DeployDocumentation()
                        }
                    }
                }
            }
        }

        stage('Export Unity Package')
        {
            when
            {
                anyOf 
                {
                    tag pattern: "v(\\d+\\.\\d+\\.\\d+)", comparator: "REGEXP"
                    expression { params.BUILD_UNITYPACKAGE == true }
                }
            }
            steps
            {
                script
                {
                    writeFile file: "${EXPORT_CONFIG}", text: $/
                    {
                        "AssetDirectory" : "Assets/Innoactive/",
                        "Version" : "${env.TAG_NAME}",
                        "OutputPath" : "./Builds/innoactive-creator-${env.TAG_NAME}.unitypackage",
                        "Excludes" : [
                            "*/Tests/*",
                            "*/VRTK/Examples/*",
                            "*/LegacyExampleFiles/*",
                            "*.sh",
                        ]
                    }
                    /$

                    load(SHARED).CreateUnityPackage(EXPORT_CONFIG)
                }
            }
        }
    }

    post
    {
        always
        {
            archiveArtifacts allowEmptyArchive: true, artifacts: "${BUILD_TAG}/*.log"
        }

        cleanup
        {
            dir("${BUILD_TAG}")
            {
                // Publish the results of the test
                nunit testResultsPattern: "TestResults*.xml", failIfNoResults: false
            }

            powershell """
            subst z: /D
            exit(0)
            """
        }
    }
}
